import java.util.Properties

val localProps = Properties().apply {
    val localPropsFile = rootProject.file("local.properties")
    if (localPropsFile.exists()) {
        localPropsFile.inputStream().use { load(it) }
    }
}

fun propOrEmpty(key: String): String = (localProps.getProperty(key) ?: "").trim()

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.compose)
}

android {
    namespace = "io.github.mhmdwaelanwr.eventcheckin"
    compileSdk = 36

    defaultConfig {
        applicationId = "io.github.mhmdwaelanwr.eventcheckin"
        minSdk = 24
        targetSdk = 36
        versionCode = 1
        versionName = "1.0"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "HEDERA_ACCOUNT_ID", "\"${propOrEmpty("HEDERA_ACCOUNT_ID")}\"")
        buildConfigField("String", "HEDERA_PRIVATE_KEY", "\"${propOrEmpty("HEDERA_PRIVATE_KEY")}\"")
        buildConfigField("String", "HEDERA_TOPIC_ID", "\"${propOrEmpty("HEDERA_TOPIC_ID")}\"")
        buildConfigField("String", "BASE_URL", "\"${propOrEmpty("BASE_URL")}\"")
        buildConfigField("String", "APP_ACCESS_KEY", "\"${propOrEmpty("APP_ACCESS_KEY")}\"")
        buildConfigField("String", "REMOTE_CONFIG_URL", "\"${propOrEmpty("REMOTE_CONFIG_URL")}\"")
    }

    buildTypes {
        release {
            isMinifyEnabled = true
            isShrinkResources = true
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }

    splits {
        abi {
            isEnable = true
            reset()
            include("arm64-v8a", "armeabi-v7a", "x86", "x86_64")
            // Avoid generating a fat APK with all ABIs.
            isUniversalApk = false
        }
    }

    bundle {
        abi {
            enableSplit = true
        }
        density {
            enableSplit = true
        }
        language {
            enableSplit = true
        }
    }

    packaging {
        resources {
            excludes += setOf(
                "META-INF/DEPENDENCIES",
                "META-INF/AL2.0",
                "META-INF/LGPL2.1"
            )
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = "11"
    }
    buildFeatures {
        compose = true
        buildConfig = true
    }
}


dependencies {
    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.lifecycle.runtime.ktx)
    implementation(libs.androidx.activity.compose)
    implementation(platform(libs.androidx.compose.bom))
    implementation(libs.androidx.compose.ui)
    implementation(libs.androidx.compose.ui.graphics)
    implementation(libs.androidx.compose.ui.tooling.preview)
    implementation(libs.androidx.compose.material3)
    implementation(libs.androidx.compose.material.icons.core)
    implementation(libs.androidx.compose.material.icons.extended)

    // Networking
    implementation(libs.retrofit)
    implementation(libs.converter.gson)

    // CameraX
    implementation(libs.androidx.camera.core)
    implementation(libs.androidx.camera.camera2)
    implementation(libs.androidx.camera.lifecycle)
    implementation(libs.androidx.camera.view)
    
    // ML Kit
    implementation(libs.mlkit.barcode.scanning)

    // Lifecycle
    implementation(libs.androidx.lifecycle.viewmodel.compose)

    // Hedera SDK
    implementation(libs.hedera.sdk)
    implementation(libs.grpc.okhttp)
    // Guava for ListenableFuture used by CameraX
    implementation(libs.guava)

    // Security - Added for EncryptedSharedPreferences
    implementation("androidx.security:security-crypto:1.1.0-alpha06")

    testImplementation(libs.junit)
    androidTestImplementation(libs.androidx.junit)
    androidTestImplementation(libs.androidx.espresso.core)
    androidTestImplementation(platform(libs.androidx.compose.bom))
    androidTestImplementation(libs.androidx.compose.ui.test.junit4)
    debugImplementation(libs.androidx.compose.ui.tooling)
    debugImplementation(libs.androidx.compose.ui.test.manifest)
}
